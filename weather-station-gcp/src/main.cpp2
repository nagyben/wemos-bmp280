#include <Arduino.h>
#include <ESP8266WiFi.h>
#include <ESP8266HTTPClient.h>
#include <WiFiClientSecureBearSSL.h>
#include <LittleFS.h>
#include <Wire.h>
#include <SPI.h>
#include <Adafruit_BME280.h>

#define DEBUG
#ifdef DEBUG
  #define DEBUG_PRINT(x)  Serial.print (x)
  #define DEBUG_PRINTLN(x)  Serial.println (x)
#else
  #define DEBUG_PRINT(x)
  #define DEBUG_PRINTLN(x)
#endif

#include <config.h>
#include <api.h>

#define BME_VCC_PIN 3
#define BATTERY_VCC_PIN A0

//global configuration object
Config config;

// uploaded to ESP's filesystem separately
// if the file is not present, the code won't do anything
const char *filename = "config.json";

BearSSL::WiFiClientSecure *client;
HTTPClient http;

Adafruit_BME280 bme;

const long DEEPSLEEP_TIME = 5 * 60 * 1e6; // microseconds
const int MAX_WIFI_CONNECT_TIME = 100 * 1e3; // milliseconds

void bmeSensorJson(DynamicJsonDocument &d);

IPAddress ip( 192, 168, 1, 132 );
IPAddress gateway( 192, 168, 1, 1 );
IPAddress subnet( 255, 255, 255, 0 );

void initWiFi(Config &config, int timeout = MAX_WIFI_CONNECT_TIME) {
  WiFi.forceSleepWake();
  yield(); // IMPORTANT!
  WiFi.persistent( false ); // Disable the WiFi persistence.  The ESP8266 will not load and save WiFi settings in the flash memory.
  WiFi.mode(WIFI_STA);
  WiFi.begin(config.ssid, config.password);
  DEBUG_PRINT("Connecting to WiFi ..");
  long start = millis();
  while (WiFi.status() != WL_CONNECTED) {
    delay(500);
    DEBUG_PRINT('.');
    if (millis() - start > timeout) {
      DEBUG_PRINTLN("WiFi failed to connect within power budget");
      DEBUG_PRINTLN("Deep sleeping...");
      ESP.deepSleep(DEEPSLEEP_TIME, WAKE_RF_DISABLED);
    }
  }
  DEBUG_PRINT("Connected, IP address: "); DEBUG_PRINTLN(WiFi.localIP());
}

void setup() {
  // =================================================
  // Serial
  // =================================================
#ifdef DEBUG
  Serial.begin(115200);
  delay(500);
#endif

  // =================================================
  // Filesystem & load config
  // =================================================
  bool fsStatus = LittleFS.begin();
  if (!fsStatus) {
    DEBUG_PRINTLN("Could not mount filesystem!");
  }
  loadConfiguration(filename, config);
  String privateKey = loadPrivateKey();
  LittleFS.end();

  if (!isConfigValid(config)) {
    return;
  }

  initWiFi(config);

  client = new BearSSL::WiFiClientSecure;
  // client->setFingerPrint(fingerprint);
  // Or, if you happy to ignore the SSL certificate, then use the following line instead:
  client->setInsecure();
  http.begin(*client, "https://192.168.1.47:5000");
  int httpStatusCode = http.POST("hello");
  String returnValue;
  if (httpStatusCode > 0)
  {
    DEBUG_PRINT(F("HTTP Response code: "));
    DEBUG_PRINTLN(httpStatusCode);
    returnValue = http.getString();
  }
  else
  {
    DEBUG_PRINT(F("Error code: "));
    DEBUG_PRINTLN(httpStatusCode);
    returnValue = http.errorToString(httpStatusCode);
  }
  // Free resources
  http.end();

  client->stop();
}

void loop() {}

void bmeSensorJson(DynamicJsonDocument &data) {
  pinMode(BME_VCC_PIN, OUTPUT);
  digitalWrite(BME_VCC_PIN, HIGH);
  if (!bme.begin()) {
    DEBUG_PRINTLN("Could not initialize BMP280 - check wiring!");
  }
  bme.setSampling(Adafruit_BME280::MODE_FORCED,
                  Adafruit_BME280::SAMPLING_X1, // temp
                  Adafruit_BME280::SAMPLING_X1, // pressure
                  Adafruit_BME280::SAMPLING_X1, // humidity
                  Adafruit_BME280::FILTER_OFF
                  );
  delay(1000);
  bme.takeForcedMeasurement();
  data["temp_C"] = bme.readTemperature();
  data["pressure_Pa"] = bme.readPressure();
  data["humidity_%"] = bme.readHumidity();
  digitalWrite(BME_VCC_PIN, LOW);
}